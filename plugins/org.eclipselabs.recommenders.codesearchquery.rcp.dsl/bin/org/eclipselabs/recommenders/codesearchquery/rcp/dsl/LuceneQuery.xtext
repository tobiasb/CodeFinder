grammar org.eclipselabs.recommenders.codesearchquery.rcp.dsl.LuceneQuery 
	with org.eclipse.xtext.common.Terminals//org.eclipse.xtext.xbase.Xtype

generate luceneQuery 'http://www.eclipselabs.org/recommenders/codesearchquery/rcp/dsl/LuceneQuery'


/* * 
 *   Query  ::= ( Clause )*
 *   Clause ::= ["+", "-"] [<TERM> ":"] ( <TERM> | "(" Query ")" )
 * */
 
	Exp1 returns Expression:
	 Exp2 ( {Exp1.left=current} b=BinaryExp? right=Exp1 )?; // Right associativity. Why? See http://blog.efftinge.de/2010/08/parsing-expressions-with-xtext.html
	
	Exp2 returns Expression:
	 value=ClauseExpression |
	 (UnaryExpression? '(' Exp1 ')');
	 
/*
	The following rules are generated. Do not modify. Modify source file instead.
*/
	ClauseExpression:
		(UnaryExpression)? 
		(
			(field=SimpleField ':' value=SimpleFieldValue)
			| (field=TypeField ':' value=TypeFieldValue)
			| (field=MethodField ':' value=MethodFieldValue)
			| (field=FilePathField ':' value=FilePathFieldValue)
			| (field=NumberField ':' value=NumberFieldValue)
			| (field=ModifierField ':' value=ModifierFieldValue)
			| (field=TimeField ':' value=TimeFieldValue)
			| (field=DocumentTypeField ':' value=DocumentTypeFieldValue)
			| (field=ProjectNameField ':' value=ProjectNameFieldValue)
			| (field=AnnotationField ':' value=AnnotationFieldValue)
		)
	;

	TypeField:
			ImplementedTypes='ImplementedTypes'
		| 	ExtendedTypes='ExtendedTypes'
		| 	UsedTypes='UsedTypes'
		| 	UsedTypesInTry='UsedTypesInTry'
		| 	UsedTypesInFinally='UsedTypesInFinally'
		| 	ParameterTypes='ParameterTypes'
		| 	ReturnType='ReturnType'
		| 	InstanceofTypes='InstanceofTypes'
		| 	AllImplementedTypes='AllImplementedTypes'
		| 	AllExtendedTypes='AllExtendedTypes'
		| 	FieldType='FieldType'
		| 	CaughtType='CaughtType'
		| 	DeclaredFieldTypes='DeclaredFieldTypes'
		| 	DeclaringType='DeclaringType'
	;
	
	MethodField:
			UsedMethods='UsedMethods'
		| 	UsedMethodsInTry='UsedMethodsInTry'
		| 	UsedMethodsInFinally='UsedMethodsInFinally'
		| 	OverriddenMethods='OverriddenMethods'
	;
	
	FilePathField:
			ResourcePath='ResourcePath'
	;
	
	NumberField:
			ParameterCount='ParameterCount'
	;
	
	ModifierField:
			Modifiers='Modifiers'
	;
	
	TimeField:
			Timestamp='Timestamp'
	;
	
	DocumentTypeField:
			Type='Type'
	;
	
	ProjectNameField:
			ProjectName='ProjectName'
	;
	
	AnnotationField:
			Annotations='Annotations'
	;
	
	SimpleField:
			FullyQualifiedName='FullyQualifiedName'
		| 	FriendlyName='FriendlyName'
		| 	DeclaredMethods='DeclaredMethods'
		| 	ReturnVariableEexpressions='ReturnVariableEexpressions'
		| 	AllDeclaredMethodNames='AllDeclaredMethodNames'
		| 	DeclaredMethodNames='DeclaredMethodNames'
		| 	DeclaredFieldNames='DeclaredFieldNames'
		| 	AllDeclaredFieldNames='AllDeclaredFieldNames'
		| 	FullText='FullText'
		| 	FieldsRead='FieldsRead'
		| 	FieldsWritten='FieldsWritten'
		| 	UsedFieldsInFinally='UsedFieldsInFinally'
		| 	UsedFieldsInTry='UsedFieldsInTry'
	;
	
	/*
	End of generated rules.
*/

		

	SimpleFieldValue:
		NameWithWC
	;
	
	MethodFieldValue:
		NameWithWC
	;
	
	ProjectNameFieldValue:
		NameWithWC
	;
	
	NumberFieldValue:
		INT
	;
	
	ModifierFieldValue:
		'private'
		| 'public' 
		| 'static' 
		| 'final'
		| 'abstract' 
		| 'protected'
	;
	
	TimeFieldValue:
		NameWithWC
	;
	
	DocumentTypeFieldValue:
		'class'
		| 'method'
		| 'field'
		| 'trycatch'
	;
	
	AnnotationFieldValue:
		NameWithWC
	;
	
	TypeFieldValue:
		NameWithWC
	;
	
	terminal NameWithWC				: ('a'..'z'|'A'..'Z'|'_'|'*'|'?') ('a'..'z'|'A'..'Z'|'_'|'0'..'9'|'*'|'?'|'.')*;
	terminal PathWithWC  			: (('A'..'Z' ':') | ('a'..'z'|'A'..'Z'|'_'|'/')) ('a'..'z'|'A'..'Z'|'_'|'0'..'9'|'*'|'?'|'.'|'/')*;
	
	FilePathFieldValue:
		PathWithWC
	;
	//-----------------
	
	enum BinaryExp:
		and1='AND' | or1='OR'
	;

	UnaryExpression:
		('-' | 'NOT' | '!' | '+')
	;

