/**
 * Copyright (c) 2012 Tobias Boehm.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 * 
 * Contributors:
 *    Tobias Boehm - initial API and implementation.
 */
 grammar org.eclipselabs.recommenders.codesearch.rcp.dslQL1.QL1 with org.eclipse.xtext.common.Terminals

generate qL1 "http://www.eclipselabs.org/recommenders/codesearch/rcp/dslQL1/QL1"


MethodPattern:
	definition=MethodPatternDefinition
;

MethodPatternDefinition:
	modifier=Modifier returnType=ReturnType? methodName=MethodName (parameterDefinition=ParameterDefinition)? throwsClause=Throws?
;

ParameterDefinition:
	{ParameterDefinition}
	'(' (parameterElementholder+=ParameterElementHolder (',' parameterElementholder+=ParameterElementHolder)*)? ')'
;

Modifier:
	{Modifier}
	(modifiers+=ModifierValue)*
;

ModifierValue:
	(value='static' 
	| value='private' 
	| value='public' 
	| value='final' 
	| value='abstract' 
	| value='protected' )
;

ReturnType:
	value=TypeFieldValue
;

ParameterElementHolder:
	element=SingleElement | element=MultiElement
;

SingleElement:
	value=ParameterType
;

MultiElement:
	'{' elements+=SingleElement ('|' elements+=SingleElement)* '}'
;

ParameterType:
	value=TypeFieldValue | value='..'
;

Throws:
	'throws' value=TypeFieldValue
;

MethodName:
	value=SimpleFieldValue
;

SimpleFieldValue:
	NameWithWC
;
	
TypeFieldValue:
	'void' | NameWithWC
;

terminal NameWithWC		: ('a'..'z'|'A'..'Z'|'_'|'*'|'?') ('a'..'z'|'A'..'Z'|'_'|'0'..'9'|'*'|'?'|'.'|'/')*;