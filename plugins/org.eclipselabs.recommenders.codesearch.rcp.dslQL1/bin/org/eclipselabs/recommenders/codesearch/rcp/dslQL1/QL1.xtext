grammar org.eclipselabs.recommenders.codesearch.rcp.dslQL1.QL1 with org.eclipse.xtext.common.Terminals

generate qL1 "http://www.eclipselabs.org/recommenders/codesearch/rcp/dslQL1/QL1"


MethodPattern:
	definition=MethodPatternDefinition
;

MethodPatternDefinition:
	(modifiers+=Modifier)* returnType=ReturnType methodName=MethodName (parameterDefinition=ParameterDefinition)? throwsClause=Throws?
;

ParameterDefinition:
	'(' parameterElementholder+=ParameterElementHolder (',' parameterElementholder+=ParameterElementHolder)* ')'
;

Modifier:
	(value='static' 
	| value='private' 
	| value='public' 
	| value='final' 
	| value='abstract' 
	| value='protected' )
;

ReturnType:
	value=NameWithWC
;

ParameterElementHolder:
	element=SingleElement | element=MultiElement
;

SingleElement:
	value=ParameterType
;

MultiElement:
	'{' elements+=SingleElement ('|' elements+=SingleElement)* '}'
;

ParameterType:
	value=NameWithWC | value='..'
;

Throws:
	'throws' value=NameWithWC
;

MethodName:
	value=NameWithWC
;
	
terminal NameWithWC		: ('a'..'z'|'A'..'Z'|'_'|'*'|'?') ('a'..'z'|'A'..'Z'|'_'|'0'..'9'|'*'|'?'|'.'|'/')*;